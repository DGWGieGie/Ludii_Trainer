;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Connect 4
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Roles
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(role P1)
(role P2)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Game Mode
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= (next (control P1)) (true (control P2)))
(<= (next (control P2)) (true (control P1)))


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Initial State
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(init (cell 1 0 dirt))
(init (cell 2 0 dirt))
(init (cell 3 0 dirt))
(init (cell 4 0 dirt))
(init (cell 5 0 dirt))
(init (cell 6 0 dirt))
(init (cell 7 0 dirt))
(init (cell 1 1 b))
(init (cell 1 2 b))
(init (cell 1 3 b))
(init (cell 1 4 b))
(init (cell 1 5 b))
(init (cell 1 6 b))
(init (cell 2 1 b))
(init (cell 2 2 b))
(init (cell 2 3 b))
(init (cell 2 4 b))
(init (cell 2 5 b))
(init (cell 2 6 b))
(init (cell 3 1 b))
(init (cell 3 2 b))
(init (cell 3 3 b))
(init (cell 3 4 b))
(init (cell 3 5 b))
(init (cell 3 6 b))
(init (cell 4 1 b))
(init (cell 4 2 b))
(init (cell 4 3 b))
(init (cell 4 4 b))
(init (cell 4 5 b))
(init (cell 4 6 b))
(init (cell 5 1 b))
(init (cell 5 2 b))
(init (cell 5 3 b))
(init (cell 5 4 b))
(init (cell 5 5 b))
(init (cell 5 6 b))
(init (cell 6 1 b))
(init (cell 6 2 b))
(init (cell 6 3 b))
(init (cell 6 4 b))
(init (cell 6 5 b))
(init (cell 6 6 b))
(init (cell 7 1 b))
(init (cell 7 2 b))
(init (cell 7 3 b))
(init (cell 7 4 b))
(init (cell 7 5 b))
(init (cell 7 6 b))
(index 0) (index 1) (index 2) (index 3) (index 4) (index 5) (index 6) (index 7) (index 8)
(++ 0 1) (++ 1 2) (++ 2 3) (++ 3 4) (++ 4 5) (++ 5 6) (++ 6 7) (++ 7 8) (init (control P1))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Legal Action
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= open (true (cell ?m ?n b)))
(<= (legal P2 noop) (true (control P1)))
(<= (legal P1 noop) (true (control P2)))
(<= (legal ?w (drop ?c)) (true (cell ?c ?y2 b)) (filled ?c ?y1) (++ ?y1 ?y2) (true (control ?w)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;  Move & Update
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= (next (cell ?c ?h2 x)) (does P1 (drop ?c)) (true (cell ?c ?h2 b)) (filled ?c ?h1) (++ ?h1 ?h2))
(<= (next (cell ?c ?h2 o)) (does P2 (drop ?c)) (true (cell ?c ?h2 b)) (filled ?c ?h1) (++ ?h1 ?h2))
(<= (next (cell ?c ?y2 b)) (true (cell ?c ?y1 b)) (distinct ?y1 6) (++ ?y1 ?y2))
(<= (next (cell ?x ?y ?z)) (true (cell ?x ?y ?z)) (distinct ?z b))
(<= (next (cell ?c2 ?y b)) (does ?w (drop ?c1)) (true (cell ?c2 ?y b)) (distinct ?c1 ?c2))
(<= (filled ?c ?h) (true (cell ?c ?h dirt)))
(<= (filled ?c ?h) (true (cell ?c ?h x)))
(<= (filled ?c ?h) (true (cell ?c ?h o)))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Win Condition
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= (row ?m ?x) (true (cell ?m 1 ?x)) (true (cell ?m 2 ?x)) (true (cell ?m 3 ?x)) (true (cell ?m 4 ?x)))
(<= (row ?m ?x) (true (cell ?m 2 ?x)) (true (cell ?m 3 ?x)) (true (cell ?m 4 ?x)) (true (cell ?m 5 ?x)))
(<= (row ?m ?x) (true (cell ?m 3 ?x)) (true (cell ?m 4 ?x)) (true (cell ?m 5 ?x)) (true (cell ?m 6 ?x)))
(<= (column ?n ?x) (true (cell 1 ?n ?x)) (true (cell 2 ?n ?x)) (true (cell 3 ?n ?x)) (true (cell 4 ?n ?x)))
(<= (column ?n ?x) (true (cell 2 ?n ?x)) (true (cell 3 ?n ?x)) (true (cell 4 ?n ?x)) (true (cell 5 ?n ?x)))
(<= (column ?n ?x) (true (cell 3 ?n ?x)) (true (cell 4 ?n ?x)) (true (cell 5 ?n ?x)) (true (cell 6 ?n ?x)))
(<= (column ?n ?x) (true (cell 4 ?n ?x)) (true (cell 5 ?n ?x)) (true (cell 6 ?n ?x)) (true (cell 7 ?n ?x)))
(<= (diagonal ?x) (true (cell 1 1 ?x)) (true (cell 2 2 ?x)) (true (cell 3 3 ?x)) (true (cell 4 4 ?x)))
(<= (diagonal ?x) (true (cell 1 2 ?x)) (true (cell 2 3 ?x)) (true (cell 3 4 ?x)) (true (cell 4 5 ?x)))
(<= (diagonal ?x) (true (cell 1 3 ?x)) (true (cell 2 4 ?x)) (true (cell 3 5 ?x)) (true (cell 4 6 ?x)))
(<= (diagonal ?x) (true (cell 2 1 ?x)) (true (cell 3 2 ?x)) (true (cell 4 3 ?x)) (true (cell 5 4 ?x)))
(<= (diagonal ?x) (true (cell 2 2 ?x)) (true (cell 3 3 ?x)) (true (cell 4 4 ?x)) (true (cell 5 5 ?x)))
(<= (diagonal ?x) (true (cell 2 3 ?x)) (true (cell 3 4 ?x)) (true (cell 4 5 ?x)) (true (cell 5 6 ?x)))
(<= (diagonal ?x) (true (cell 3 1 ?x)) (true (cell 4 2 ?x)) (true (cell 5 3 ?x)) (true (cell 6 4 ?x)))
(<= (diagonal ?x) (true (cell 3 2 ?x)) (true (cell 4 3 ?x)) (true (cell 5 4 ?x)) (true (cell 6 5 ?x)))
(<= (diagonal ?x) (true (cell 3 3 ?x)) (true (cell 4 4 ?x)) (true (cell 5 5 ?x)) (true (cell 6 6 ?x)))
(<= (diagonal ?x) (true (cell 4 1 ?x)) (true (cell 5 2 ?x)) (true (cell 6 3 ?x)) (true (cell 7 4 ?x)))
(<= (diagonal ?x) (true (cell 4 2 ?x)) (true (cell 5 3 ?x)) (true (cell 6 4 ?x)) (true (cell 7 5 ?x)))
(<= (diagonal ?x) (true (cell 4 3 ?x)) (true (cell 5 4 ?x)) (true (cell 6 5 ?x)) (true (cell 7 6 ?x)))
(<= (diagonal ?x) (true (cell 1 4 ?x)) (true (cell 2 3 ?x)) (true (cell 3 2 ?x)) (true (cell 4 1 ?x)))
(<= (diagonal ?x) (true (cell 1 5 ?x)) (true (cell 2 4 ?x)) (true (cell 3 3 ?x)) (true (cell 4 2 ?x)))
(<= (diagonal ?x) (true (cell 1 6 ?x)) (true (cell 2 5 ?x)) (true (cell 3 4 ?x)) (true (cell 4 3 ?x)))
(<= (diagonal ?x) (true (cell 2 4 ?x)) (true (cell 3 3 ?x)) (true (cell 4 2 ?x)) (true (cell 5 1 ?x)))
(<= (diagonal ?x) (true (cell 2 5 ?x)) (true (cell 3 4 ?x)) (true (cell 4 3 ?x)) (true (cell 5 2 ?x)))
(<= (diagonal ?x) (true (cell 2 6 ?x)) (true (cell 3 5 ?x)) (true (cell 4 4 ?x)) (true (cell 5 3 ?x)))
(<= (diagonal ?x) (true (cell 3 4 ?x)) (true (cell 4 3 ?x)) (true (cell 5 2 ?x)) (true (cell 6 1 ?x)))
(<= (diagonal ?x) (true (cell 3 5 ?x)) (true (cell 4 4 ?x)) (true (cell 5 3 ?x)) (true (cell 6 2 ?x)))
(<= (diagonal ?x) (true (cell 3 6 ?x)) (true (cell 4 5 ?x)) (true (cell 5 4 ?x)) (true (cell 6 3 ?x)))
(<= (diagonal ?x) (true (cell 4 4 ?x)) (true (cell 5 3 ?x)) (true (cell 6 2 ?x)) (true (cell 7 1 ?x)))
(<= (diagonal ?x) (true (cell 4 5 ?x)) (true (cell 5 4 ?x)) (true (cell 6 3 ?x)) (true (cell 7 2 ?x)))
(<= (diagonal ?x) (true (cell 4 6 ?x)) (true (cell 5 5 ?x)) (true (cell 6 4 ?x)) (true (cell 7 3 ?x)))
(<= (line ?x) (row ?m ?x))
(<= (line ?x) (column ?n ?x))
(<= (line ?x) (diagonal ?x))

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; Reward
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= (goal P1 100) (line x))
(<= (goal P2 100) (line o))
(<= (goal P1 50) (not (line x)) (not (line o)) (not open))
(<= (goal P2 50) (not (line x)) (not (line o)) (not open))
(<= (goal P1 0) (line o))
(<= (goal P2 0) (line x))
(<= (goal ?w 70) (role ?w) (not (line x)) (not (line o)) open)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; End
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
(<= terminal (line x))
(<= terminal (line o))
(<= terminal (not open))